plugins {
    id 'com.android.library'
    id "maven-publish"
}
android {
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 1
        versionName "1.0"
        compileSdk 31

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
    namespace = "com.github.penfeizhou.animation.frameanimation"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.2.0'

    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
//apply from: rootProject.file('scripts/upload.gradle')

def loadProperties(filename) {
    def propertiesFile = file(filename)
    if (!propertiesFile.exists()) {
        throw new IllegalArgumentException("Can't find the property file from $filename.")
    }
    Properties props = new Properties()
    props.load(new FileInputStream(propertiesFile))
    return props
}

def loadLibVersion() {
    def props = loadProperties("$rootDir/version.properties")
    return props["version"]
}

def libVersion = loadLibVersion()

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                // You can then customize attributes of the publication as shown below.
                groupId = "com.github.wangyung"
                artifactId = "frameanimation"
                version = libVersion
            }
        }
    }
}